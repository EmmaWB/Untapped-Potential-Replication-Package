/*

File: 05 - Analyze Digital Trace Activity Data
								
Authors: 		Erin Macke, Claire Daviss, and Emma Williams Baron (equal authorship)
Date:			June 25, 2024
Project: 		Study 1 in "Untapped Potential: Designed Digital Trace Data in Online Survey Experiments"
Description:	This file uses the long analysis data generated by prior do-files to 
					run analyses.
*/




********************************************************************************
* Preparing environment                                                        *
********************************************************************************

* Setting filepath globals
global wd "UPDATE HERE WITH YOUR WORKING DIRECTORY FILEPATH"		// update with your own working directory filepath here
global do "$wd/do"
global data "$wd/data"
global output "$wd/output"
set scheme s2mono

* Setting log
cap log close
log using "$do/log/05_analyze.log", replace

* Clearing environment
clear all

* Load data
use "$data/analysis_data_long.dta.", clear

********************************************************************************
* Conduct analyses                                                             *
********************************************************************************

* Use regression to determine statistical significance of difference between in-person and remote total time in deciles
reg tottime_deciles i.jobtype if woman == 1 // p < 0.01
reg tottime_deciles i.jobtype if woman == 0 // p < 0.1

* Collapse the data to generate the 95% confidence intervals
preserve
collapse (mean) meantime = tottime_deciles (sd) sdtime = tottime_deciles ///
	(count) n = tottime_deciles, by(jobtype man)
gen ul = meantime + invttail(n-1, 0.025)*(sdtime / sqrt(n)) // storing the upper limits of confidence intervals
gen ll = meantime - invttail(n-1, 0.025)*(sdtime / sqrt(n)) // storing the lower limits of confidence intervals

* Graph the data for women
graph twoway ///
	(bar meantime jobtype if man==0 & jobtype==1, color(gs4)) ///
	(bar meantime jobtype if man==0 & jobtype==2, color(gs8)) ///
	(bar meantime jobtype if man==0 & jobtype==3, color(gs12)) ///
	(rcap ul ll jobtype if man==0) ///
	(pci 6.2 1 6.2 3, lcolor(black)) ///
	(pci 6.15 1 6.2 1, lcolor(black)) ///
	(pci 6.15 3 6.2 3, lcolor(black)) ///
	(pci 6.2 2 6.25 2, lcolor(black)), ///
	ti("Women Candidates" " ") ///
	yti("") ylab(, nogrid) ///
	ylab(4.5(0.5)6) ysc(range(4.5 6.4)) ///
	xlab(1 "In-Person" 2 "Hybrid" 3 "Remote", tick)  ///
	text(6.3 2 "** p < .01") ///
	graphregion(color(white)) legend(off) /// 
	name(timing_wom, replace)

* Graph the data for men
graph twoway ///
	(bar meantime jobtype if man==1 & jobtype==1, color(gs4)) ///
	(bar meantime jobtype if man==1 & jobtype==2, color(gs8)) ///
	(bar meantime jobtype if man==1 & jobtype==3, color(gs12)) ///
	(rcap ul ll jobtype if man==1) ///
	(pci 6.2 1 6.2 3, lcolor(black)) ///
	(pci 6.15 1 6.2 1, lcolor(black)) ///
	(pci 6.15 3 6.2 3, lcolor(black)) ///
	(pci 6.2 2 6.25 2, lcolor(black)), ///
	ti("Men Candidates" " ") ///
	yti("") ylab(, nogrid) ///
	ylab(4.5(0.5)6) ysc(range(4.5 6.4)) ///
	xlab(1 "In-Person" 2 "Hybrid" 3 "Remote", tick)  ///
	text(6.3 2 "+ p < .1") ///
	graphregion(color(white)) legend(off) /// 
	name(timing_men, replace)
restore


* Combine women's and men's graphs into one: this is the final figure shown in the paper
graph combine timing_wom timing_men, graphregion(color(white))
graph export "$output/Figure2_PredictedTimeResumes_ByGenderJobType.png", as(png) replace

log close

